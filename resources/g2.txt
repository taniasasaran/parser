<S'> ::= <program>

<program> ::= <stmtlist>

<stmtlist> ::= <stmt> | <stmt> ";" <stmtlist>

<stmt> ::= <simplstmt> | <structstmt>

<simplstmt> ::= <assignstmt> | <iostmt> | <declstmt>

<declstmt> ::= <type> <IDENTIFIER> | <type> <IDENTIFIER> "[" <nr> "]"

<type> ::= "char" | "int" | "string"

<assignstmt> ::= <IDENTIFIER> "=" <expression>

<expression> ::= <expression> "+" <term> | <expression> "-" <term> | <term>

<term> ::= <term> "*" <factor> | <term> "/" <factor> | <factor>

<factor> ::= <IDENTIFIER> | "(" <expression> ")" | <nr>

<iostmt> ::= <readstmt> | <writestmt>

<readstmt> ::= "READ" "(" <IDENTIFIER> ")"

<writestmt> ::= "WRITE" "(" <expression> ")"

<structstmt> ::= <ifestmt> | <whilestmt>

<ifstmt> ::= "IF" <condition> "{" <stmt> "}"

<elsestmt> ::= "ELSE" "{" <stmt> "}"

<ifestmt> ::= <ifstmt> | <ifstmt> <elsestmt>

<whilestmt> ::= "WHILE" <condition> "{" <stmt> "}"

<condition> ::= <expression> <RELATION> <expression>

<RELATION> ::= "<" | "<=" | "==" | "<>" | ">=" | ">"

<IDENTIFIER> ::= "a" | "b" | "c"

<nr> ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"